"
The factorial function is defined on n >= 0.
This one just returns 0 for negative arguments.
"
fun factorial(n)
vars total
{       
    total = 1;

    while (n > 1) 
    {       
        total (*)= n;
        n (-)= 1;
    }
    
    total*(n >= 0)
}

"
 Fibonacci Sequence,
 Works with negative arguments.
"
fun fibonacci(n)
vars tmp, pred, prepred, sign
{
    if (n < 0) then 
    { 
        n = -n;
        sign = (n%2)*2 - 1;
    }
    else 
    {
        sign = 1;
    } 
    pred = 1;
    prepred = 0;

    while (n != 0)
    {
        tmp = pred;
        pred (+)= prepred;
        prepred = tmp;
        
        n (-)= 1;
    }

    prepred * sign
}


"Number of permutations of r things from n things."
fun perm(n, r)
{
    factorial(n) / factorial(n - r)
}

"The binomial coefficient; n choose r."
fun  binom(n, r)
{   
    perm(n,r) / factorial(r)
}

"Simply an alias."
fun choose(n, r) { binom(n, r) }


"
 The catalan numbers can represent, among many other examples
 the number of strings of 2n balanced brackets
"
fun catalan(n)
{
    binom(2*n, n) / (n+1)
}

"Returns the greatest integer less than or equal to the square root of n."
fun sqrt(n)
vars val
{

    while (val^2 <= n)
    {
        val (+)= 1;
    }

    val - 1
}

"Determines if a number is prime by simple trial division."
fun isPrime(n)
vars max, factor
{
    max = sqrt(n);
    factor = 2;

    while (factor <= max) 
    {
        if !(n % factor) then
        {
            return false;
        }

        factor (+)= 1;
    } 

    n > 1
}

"
 Generates the n'th prime by checking each successive integer.
 Super slow and crummy, but it's hard to write a sieve without lists.
 There are good non-sieve primality tests but I forgot how they work. 
"
fun prime(n)
vars pcount, cand
{
    if (n < 1) then
    {
        return 0;
    }

    cand = 1;

    while (pcount < n) 
    {
        cand (+)= 1;
        pcount (+)= isPrime(cand);
    }

    cand
}

"
 True iff m and n share no common factors.
"
fun coprime(m, n)
vars f
{
    f = m [gcd] n;

    while f != 1 {
        if f==m | f==n
        {
            return false;
        }

        m, n = m/f, n/f;
    }

    return true;
}


"Returns the number of prime divisors of n, including multiplicity."
fun numPrimeDivisors(n) 
vars i, p, k
{
    i = 1;
    p = prime(i);

    while (n > 1) {
        if !(n % p) then
        {
            k (+)= 1;
            n (/)= p;
        }
        else
        {
            i (+)= 1;
            p = prime(i);
        }
    }

    k
}


"
 Euler's totient function: returns the number of natural numbers
 less than n which are coprime with it.
"
fun phi(n) 
vars i, p, k, pPhi, result
{
	i = 1;
	p = prime(i);
	result = 1;

	while (n > 1) {
		if !(n % p) then
		{
			k (+)= 1;
			n (/)= p;
		}
		else
		{
			result (*)= (k > 0) * p^(k-1) * (p-1);
			k = !(k > 0);

			i (+)= 1;
			p = prime(i);
		}
	}

	result * (k > 0) * p^(k-1) * (p-1)
}


"GCD by the euclidian algorithm."
fun gcd(a,b)
{
	?( !(a%b),
	   b,
	   @(b, a%b)
	)
}

"Also equivalent to (n [choose] 2)."
fun triangular(n)
{
	(n+1) * n / 2
}


"
 The Ackermann function grows very quickly indeed.
 Exercise prudence with its arguments.
"
fun ackermann(m, n)
{
	?( !m,
	   n+1,
	   ?( !n,
	      @(m-1, 1),
	      @(m-1, @(m, n-1))
	    )
	 )
}


"
 Knuth's up-arrow operation.
 uparrow(a,b,p) is to uparrow(a,b,p-1) as a^b is to a*b
 uparrow(a,b,1) is simply a^b.
"
fun uparrow(a,b,p)
{
	?( p==1,
	   a^b,
	   ?( b==1,
	      a,
	      @(a, @(a,b-1,p), p-1)
	    )
	 )
}

